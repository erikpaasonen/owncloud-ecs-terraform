locals {
  public_key_material = length(var.ssh_public_key_material) == 0 ? tls_private_key.owncloud[0].public_key_openssh : var.ssh_public_key_material
}

resource aws_acm_certificate owncloud {
  count = local.custom_domain_used ? 1 : 0
  
  domain_name = "owncloud.${var.r53_domain_name}"
  validation_method = "DNS"

  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_key_pair" "deployer" {
  key_name   = "deployer-key-${random_pet.this.id}"
  public_key = local.public_key_material
}

resource tls_private_key owncloud {
  count = local.custom_ssh_key_material_provided ? 0 : 1

  algorithm = "RSA"
  rsa_bits  = 4096
}

resource aws_ssm_parameter owncloud_ssh_priv_key {
  count = local.custom_ssh_key_material_provided ? 0 : 1

  name        = "/creds/owncloud_test/${random_pet.this.id}/ssh_priv_key_material"
  description = "SSH private key generated by Terraform for the OwnCloud test activity"

  type      = "SecureString"
  value     = tls_private_key.owncloud[0].private_key_pem
  overwrite = true
}

resource aws_kms_key owncloud {
}

resource aws_kms_alias owncloud {
  name_prefix   = "alias/owncloud-${random_pet.this.id}-"
  target_key_id = "${aws_kms_key.owncloud.key_id}"
}
