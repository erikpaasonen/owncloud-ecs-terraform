// this is unfortunately a pet, not cattle, so let's have fun with that fact
resource random_pet owncloud {
  keepers = {
    deploy_version     = "v0.0.1"
    deploy_description = "updates"
    ami_id             = data.aws_ami.ubuntu_18_04.id
    vpc_id             = module.vpc.vpc_id
    ssh_key            = tls_private_key.owncloud.public_key_fingerprint_md5
  }
}

resource tls_private_key owncloud {
  algorithm = "RSA"
  rsa_bits  = 4096
}

resource "aws_key_pair" "deployer" {
  key_name   = "deployer-key-${random_pet.owncloud.id}"
  public_key = tls_private_key.owncloud.public_key_openssh
}

resource aws_ssm_parameter owncloud_ssh_priv_key {
  name        = "/creds/owncloud_test/${random_pet.owncloud.id}/ssh_priv_key_material"
  description = "SSH private key generated by Terraform for the OwnCloud test activity"

  type      = "SecureString"
  value     = tls_private_key.owncloud.private_key_pem
  overwrite = true
}
